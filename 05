import run;

fn main() run.day 5 shared part_1 part_2;

fn shared(input) {
    parts := input split "\n\n" map (\(part) part split "\n") collect;
    ranges := parts[0]
        map (\(range) range split "-" map int.1 collect)
        map (\(range) range[0]: range[1])
        collect
        sort_by_key \(range) range.a;
    ids := parts[1]
        map int.1
        collect
        sort;
    
    last := -1;
    for i in 0 to (ranges len) {
        ranges[i].a max= last + 1;
        last max= ranges[i].b;
    }

    ranges = ranges
        filter (\(range) range.a <= range.b)
        collect;

    return Database(ranges, ids);
}

fn part_1(db) {
    i := 0;
    total := 0;
    for id in db.ids take_while (<= db.ranges[-1].b) {
        while id > db.ranges[i].b {
            i += 1;
        }
        total += id >= db.ranges[i].a int;
    }
    return total;
}

fn part_2(db) db.ranges map (\(range) range.b - range.a + 1) sum;

rec Database(ranges, ids);
