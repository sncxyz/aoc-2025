import run;

fn main() run.day 7 parse part_1 part_2;

fn parse(input) input
    split "\n"
    collect;

fn part_1(manifold) {
    width := manifold[0] len;
    s := manifold[0] position (\(c) c == 'S');
    beams := [false] * width;
    beams[s] = true;
    total := 0;
    for layer in manifold skip 1 {
        for i in 0 to width {
            if layer[i] == '^' and beams[i] {
                beams[i - 1] = true;
                beams[i + 1] = true;
                beams[i] = false;
                total += 1;
            }
        }
    }
    return total;
}

fn part_2(manifold) {
    width := manifold[0] len;
    s := manifold[0] position (\(c) c == 'S');
    counts := [1] * width;
    for layer in manifold skip 1 rev {
        for i in 0 to width {
            if layer[i] == '^' {
                counts[i] = counts[i - 1] + counts[i + 1];
            }
        }
    }
    return counts[s];
}